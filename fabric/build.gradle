plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '1.4-SNAPSHOT'
}
base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}
repositories {
    maven { url "https://maven.flashyreese.me/releases" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.terraformersmc.com/" }
}
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        it.officialMojangMappings()
        it.parchment("org.parchmentmc.data:${project.fabric_parchment_version}@zip")
    }
    modImplementation("net.fabricmc:fabric-loader:${project.fabric_loader_version}")
    include(modImplementation(fabricApi.module("fabric-game-rule-api-v1", project.fabric_version)))
    include(modImplementation(fabricApi.module("fabric-key-binding-api-v1", project.fabric_version)))
    include(implementation("net.caffeinemc:CaffeineConfig:${project.caffeine_config_version}"))
    include(implementation("com.h2database:h2:${project.h2_version}"))
    include(implementation("com.moandjiezana.toml:toml4j:${project.toml4j_version}"))
    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
}
loom {
    accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.aw"))
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}
tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
processResources {
    from project(":common").sourceSets.main.resources
}