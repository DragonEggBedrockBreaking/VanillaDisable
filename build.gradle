plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
}

loom {
	accessWidenerPath = file("src/main/resources/vanilla_disable.aw")
}

repositories {
	maven { url "https://maven.flashyreese.me/releases" }
	maven { url "https://jitpack.io" }
	maven { url "https://maven.terraformersmc.com/" }
	maven { url 'https://maven.wispforest.io' }
	maven { url "https://maven.parchmentmc.org" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		it.officialMojangMappings()
		it.parchment("org.parchmentmc.data:${project.parchment_version}@zip")
	}
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	include(modImplementation(fabricApi.module("fabric-game-rule-api-v1", project.fabric_version)))
	include(modImplementation(fabricApi.module("fabric-key-binding-api-v1", project.fabric_version)))
	include(modImplementation("net.caffeinemc:CaffeineConfig:${project.caffeine_config_version}"))
	include(modImplementation("com.github.Fallen-Breath:conditional-mixin:${project.conditional_mixin_version}"))
	include(modImplementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}")))
	include(modImplementation("com.h2database:h2:${project.h2_version}"))
	include(modImplementation("com.github.ben-manes.caffeine:caffeine:${project.caffeine_version}"))
	include(modImplementation("com.moandjiezana.toml:toml4j:${project.toml4j_version}"))
	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
	modImplementation("io.wispforest:owo-lib:${project.owo_version}")
}

base {
	archivesName = archives_base_name + "-mc" + minecraft_version + "-" + mod_version
}

processResources {
	inputs.property "version", mod_version
	filesMatching("fabric.mod.json") {
		expand "version": mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${archives_base_name}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "vanilla-disable"
	versionName = mod_version + " for " + minecraft_version
	versionNumber = mod_version
	versionType = "release"
	uploadFile = remapJar
	gameVersions.add("1.20")
	gameVersions.add("1.20.1")
	loaders.add("fabric")
	loaders.add("quilt")
	dependencies {
		optional.project "modmenu"
		optional.project "owo-lib"
	}
}